# WIP
# Maintainer: Francesco Minnocci <ascoli dot minnocci at gmail dot com>

pkgname=soryu-git
_pkgname=soryu
pkgver=r7.6211043
pkgrel=1
license=('Unlicense')
pkgdesc='Glitch an image in the terminal'
makedepends=("git" "go")
conflicts=('soryu')
provides=('soryu')
arch=("x86_64")
_gourl='github.com/enjuus/soryu'
url="https://${_gourl}"
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')


pkgver() {
	cd "$srcdir/${_pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"

  export GOPATH="${srcdir}"
  go build \
   -trimpath \
   -buildmode=pie \
   -mod=readonly \
   -modcacherw \
   -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
   -o soryu main.go
  go clean -modcache
}

package() {
  cd "${srcdir}/${_pkgname}"

  install -Dm755 -t "${pkgdir}/usr/bin" ./soryu
}
# vim:set ts=2 sw=2 et:
