# Maintainer: Francesco Minnocci <ascoli dot minnocci at gmail dot com>

pkgname=lf-bachoseven-git
_pkgname=lf
pkgver=r18.r17.g1e94f45
pkgrel=1
license=('MIT')
pkgdesc='lf is a terminal file manager written in Go'
depends=("glibc")
makedepends=("git" "go>=1.6")
arch=("i686" "x86_64")
_gourl='github.com/gokcehan/lf'
url="https://${_gourl}"
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')
provides=("lf")
conflicts=("lf")

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export GOPATH="${srcdir}"/go
  cd "${srcdir}/${_pkgname}"
  go mod vendor
  version=r$pkgver ./gen/build.sh -mod=vendor -trimpath
  # clean now to ensure makepkg --clean works
  go clean -modcache
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -Dm755 -t "${pkgdir}/usr/bin" ./lf

  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" ./LICENSE
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" ./README.md ./etc/lfrc.example
  install -Dm644 -t "${pkgdir}/usr/share/${pkgname}" ./etc/lfcd.sh
  install -Dm644 -t "${pkgdir}/usr/share/man/man1" ./lf.1

  # vim
  install -Dm644 -t "${pkgdir}/usr/share/vim/vimfiles/syntax" ./etc/lf.vim
  install -Dm644 -t "${pkgdir}/usr/share/vim/vimfiles/ftdetect" ./etc/lf.vim

  # fish
  install -Dm644 -t "${pkgdir}/usr/share/fish/vendor_completions.d" ./etc/lf.fish
  install -Dm644 -t "${pkgdir}/usr/share/fish/vendor_functions.d" ./etc/lfcd.fish
}
# vim:set ts=2 sw=2 et:
