# TODO wait for setuptools >=65 in repos
# Maintainer: Francesco Minnocci <ascoli dot minnocci at gmail dot com>

pkgname=miniplayer-git
_pkgname=miniplayer
pkgver=1.7.1.r0.gf7b99d5
pkgrel=1
pkgdesc='A curses based mpd client with basic functionality and album art written for the Kitty terminal.'
url='https://github.com/guardkenzie/miniplayer'
depends=('python' 'python-mpd2' 'python-pillow' 'python-pixcat' 'ueberzug' 'python-colorthief')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel')
provides=("miniplayer")
conflicts=("miniplayer")
license=('MIT')
arch=('any')
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cd "${srcdir}/${_pkgname}"

  python -m build --wheel --no-isolation
}


package() {
  cd "${srcdir}/${_pkgname}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
# vim:set ts=2 sw=2 et:
