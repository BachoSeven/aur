# Maintainer: Francesco Minnocci <ascoli dot minnocci at gmail dot com>

pkgname=kotatogram-dev-git
_pkgname=kotatogram-desktop
pkgver=r10043.7a67d7e1b
pkgrel=1
pkgdesc="Kotatogram â€“ experimental Telegram Desktop fork - Dev branch"
arch=(x86_64)
url="https://github.com/kotatogram/kotatogram-desktop"
license=(GPL3)
depends=(
	qt5-base
	qt5-imageformats
	ffmpeg
	openal
	xz
	lz4
	xxhash
	zlib
	minizip
	openssl
	libdbusmenu-qt5
  fuse
	hunspell
)
makedepends=(
	git
	python
	cmake
	ninja
	tl-expected
	range-v3
)
optdepends=(
	'ttf-opensans: default Open Sans font family'
)
conflicts=('kotatogram-desktop')
provides=('kotatogram-desktop')
source=(
  "${_pkgname}::git+${url}.git"
)
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd ${_pkgname}

	cmake -B build -G Ninja . \
    -DCMAKE_C_FLAGS="-Werror" \
    -DCMAKE_CXX_FLAGS="-Werror" \
    -DCMAKE_EXE_LINKER_FLAGS="-s" \
		-DTDESKTOP_API_TEST=ON \
    -DDESKTOP_APP_USE_PACKAGED=OFF \
    -DDESKTOP_APP_DISABLE_CRASH_REPORTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	cd ${_pkgname}

	DESTDIR="$pkgdir" cmake --install build
}
# vim:set ts=2 sw=2 et:
